<canvas id="canvas" height="64" width="64"></canvas>
<script type="x-shader/x-vertex" id="particle-update-vert">#version 300 es
precision mediump float;

uniform float u_TimeDelta;
uniform float u_TotalTime;
uniform float u_SensorOffsetDst;
uniform int u_SensorSize;
uniform float u_SensorAngleRad;
uniform float u_TurnSpeed;
uniform vec2 u_ImgSize;

uniform sampler2D state;

in vec2 i_Position;
in vec2 i_Velocity;

out vec2 v_Position;
out vec2 v_Velocity;

// Hash function www.cs.ubc.ca/~rbridson/docs/schechter-sca08-turbulence.pdf
// https://github.com/SebLague/Slime-Simulation/blob/main/Assets/Scripts/Slime/SlimeSim.compute
uint hash(uint state)
{
    state ^= 2747636419u;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    return state;
}

float scaleToRange01(uint state)
{
    return float(state) / 4294967295.0;
}


float saturate(float x)
{
    return max(0.0, min(1.0, x));
}

float sense(vec2 agentPosition, float agentAngle, float sensorAngleOffset) {
	float sensorAngle = agentAngle + sensorAngleOffset;
	vec2 sensorDir = vec2(cos(sensorAngle), sin(sensorAngle));

	vec2 sensorPos = agentPosition + sensorDir * u_SensorOffsetDst;
	int sensorCentreX = int((sensorPos.x + 1.0)*u_ImgSize.x*0.5);
	int sensorCentreY = int((sensorPos.y + 1.0)*u_ImgSize.y*0.5);

	float sum = 0.0;

	//int4 senseWeight = agent.speciesMask * 2 - 1;

    // (sensorSize x 2) x (sensorSize x 2)
	for (int offsetX = -u_SensorSize; offsetX <= u_SensorSize; offsetX ++) {
		for (int offsetY = -u_SensorSize; offsetY <= u_SensorSize; offsetY ++) {
            int sampleX = min(int(u_ImgSize.x)-1, max(0, sensorCentreX + offsetX));
            int sampleY = min(int(u_ImgSize.y)-1, max(0, sensorCentreY + offsetY));
            sum += texture(state, vec2(float(sampleX) / u_ImgSize.x, float(sampleY) / u_ImgSize.y)).x;

			//sum += dot(senseWeight, TrailMap[int2(sampleX,sampleY)]);
            //sum += dot(senseWeight, TrailMap[int2(sampleX,sampleY)]);
		}
	}

	return sum;
}

void main() {
  vec2 newVel = normalize(i_Velocity);
  if (i_Position.x < -1.0 && newVel.x < 0.0) {
      newVel.x = -newVel.x;
  }
  if (i_Position.x > 1.0 && newVel.x > 0.0) {
    newVel.x = -newVel.x;
  }
  if (i_Position.y < -1.0 && newVel.y < 0.0) {
      newVel.y = -newVel.y;
  }
  if (i_Position.y > 1.0 && newVel.y > 0.0) {
      newVel.y = -newVel.y;
  }

  float angle = atan(newVel.y, newVel.x);

  float weightForward = sense(i_Position, angle, 0.0);
  float weightLeft = sense(i_Position, angle, -u_SensorAngleRad);
  float weightRight = sense(i_Position, angle, u_SensorAngleRad);

  uint random = hash(uint(i_Position.y * u_ImgSize.x + i_Position.x) + hash(uint(gl_VertexID) + uint(u_TotalTime)));
  float randomSteerStrength = scaleToRange01(random);

  // Continue in same direction
    if (weightForward > weightLeft && weightForward > weightRight) {
        angle += 0.0;
    }
    /*else if (weightForward < weightLeft && weightForward < weightRight) {
        angle += (randomSteerStrength - 0.5) * 2.0 * u_TurnSpeed * u_TimeDelta;
    }*/
    // Turn right
    else if (weightRight > weightLeft) {
        angle -= randomSteerStrength * u_TurnSpeed * u_TimeDelta;
    }
    // Turn left
    else if (weightLeft > weightRight) {
        angle += randomSteerStrength * u_TurnSpeed * u_TimeDelta;
    }
    newVel = vec2(cos(angle), sin(angle))*0.3;

  vec2 newPos = i_Position + newVel * u_TimeDelta;
  
  v_Position = newPos;
  v_Velocity = newVel;
  
}
</script>
<script type="x-shader/x-fragment" id="passthru-frag-shader">#version 300 es
precision mediump float;
void main() { discard; }
</script>
<script type="x-shader/x-vertex" id="particle-render-vert">#version 300 es
precision mediump float;

in vec2 i_Position;
in vec2 i_Velocity;

void main() {
    gl_PointSize = 1.0;
    // disable optimization??
    if (i_Velocity.x > 1000000.0) {
        gl_Position = vec4(i_Position, 0.0001, 1.0);
    } else {
        gl_Position = vec4(i_Position, 0.0, 1.0);
    }
}
</script>
<script type="x-shader/x-fragment" id="particle-render-frag">#version 300 es
precision mediump float;

uniform int u_SteckMode;

out vec4 o_FragColor;

void main() {
    if (u_SteckMode == 0) {
        o_FragColor = vec4(1.0);
    } else {
        o_FragColor = vec4(1.0, 1.0, 0.5, 1.0);
    }
}
</script>
<script type="x-shader/x-fragment" id="fragment-shader-display">#version 300 es
    precision mediump float;
    uniform vec2 ImgSize;
    uniform sampler2D state;
    out vec4 FragColor;
    void main(void) {
      vec2 coord = vec2(gl_FragCoord.x/ImgSize.x, 1.0 - gl_FragCoord.y/ImgSize.y);
      FragColor = texture(state, coord);
    }
  </script>
<script type="x-shader/x-fragment" id="fragment-shader-stepper">#version 300 es
    precision mediump float;
        
    // our texture
    uniform vec2 ImgSize;
    uniform sampler2D previousState;

    // calculated in here
    out vec4 FragColor;

    const float diffuseRate = 3.0;
    const float deltaTime = 0.1;
    const float decayRate = 0.30000;

    float saturate(float x)
    {
        return max(0.0, min(1.0, x));
    }


    void Diffuse (vec2 pos)
    {
        if (pos.x < 0.0 || pos.x >= ImgSize.x || pos.y < 0.0 || pos.y >= ImgSize.y) {
            return;
        }

        vec4 originalCol = texture(previousState, vec2(pos.x / ImgSize.x, pos.y / ImgSize.y));
        vec4 sum = vec4(0.0, 0.0, 0.0, 0.0);
        // 3x3 blur
        for (int offsetX = -1; offsetX <= 1; offsetX ++) {
            for (int offsetY = -1; offsetY <= 1; offsetY ++) {
                int sampleX = min(int(ImgSize.x)-1, max(0, int(pos.x) + offsetX));
                int sampleY = min(int(ImgSize.y)-1, max(0, int(pos.y) + offsetY));
                sum += texture(previousState, vec2(float(sampleX) / ImgSize.x, float(sampleY) / ImgSize.y));
            }
        }

        vec4 blurredCol = sum / 9.0;
        float diffuseWeight = saturate(diffuseRate * deltaTime);
        blurredCol = originalCol * (1.0 - diffuseWeight) + blurredCol * (diffuseWeight);

        //DiffusedTrailMap[id.xy] = blurredCol * saturate(1 - decayRate * deltaTime);
        FragColor = vec4(
            max(0.0, blurredCol.x - decayRate * deltaTime),
            max(0.0, blurredCol.y - decayRate * deltaTime),
            max(0.0, blurredCol.z - decayRate * deltaTime),
            1
        );
    }
        
    void main() {
        // Look up a color from the texture.
        // FragColor = texture(u_image, v_texCoord);
        Diffuse(vec2(gl_FragCoord.x, gl_FragCoord.y));
    }
  </script>
<script src="index.js"></script>
<br />
<button onclick="update()">Manual Update</button>
<div>
    <span>u_SensorOffsetDst(<span id="sensorOffsetDstValue">0.185</span>)</span><input type="range" min="1" max="200"
        value="185" class="slider" id="sensorOffsetDst">
    <span>u_SensorSize(<span id="sensorSizeValue">5</span>)</span><input type="range" min="1" max="10" value="5"
        class="slider" id="sensorSize">
    <span>u_SensorAngleRad(<span id="sensorAngleRadValue">0.91</span>)</span><input type="range" min="1" max="140"
        value="91" class="slider" id="sensorAngleRad">
    <span>u_TurnSpeed(<span id="turnSpeedValue">39775.0</span> * DeltaTime)</span><input type="range" min="1" max="50000" value="39775"
        class="slider" id="turnSpeed">
</div>
<script>
    [
        {input: 'sensorOffsetDst', value: 'sensorOffsetDstValue', factor: 0.001},
        {input: 'sensorSize', value: 'sensorSizeValue', factor: 1},
        {input: 'sensorAngleRad', value: 'sensorAngleRadValue', factor: 0.01},
        {input: 'turnSpeed', value: 'turnSpeedValue', factor: 1},
    ].forEach((conf) => {
        document.getElementById(conf.input).onchange = function() {
            document.getElementById(conf.value).innerHTML = parseInt(document.getElementById(conf.input).value)*conf.factor
        }
    });
</script>
<div>
    <span>Steck Mode ðŸ˜Ž</span><input type="checkbox" id="steckmode" name="steckmode">
</div>